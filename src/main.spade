use std::ops::div_pow2;

enum Output<T> {
    UpdateMode,
    FrameInv{val: bool},
    AllClear,
    Dummy,
    Address{b: bool},
    Pixel{p: T},
    // Chip select should be high but sclk should be masked
    CsHigh,
    // Chip select should be low but sclk should be masked
    CsLow
}

struct DisplayGenOutput {
    sclk: bool,
    o: Output<(int<10>, int<9>)>
}

/// Generates the base otuput signals which should then be converted
/// to contain the graphics and passed to the output translator
entity display_gen(clk: clock, rst: bool, t: Timing) -> DisplayGenOutput {
    let Clk$(sclk, sclk_falling_edge) = inst clk_gen(clk, rst, t);

    let state = inst display_fsm$(clk, rst, sclk_falling_edge, t);

    DisplayGenOutput(sclk, display_output(state))
}

struct OutputPins {
    sclk: bool,
    cs: bool,
    mosi: bool,
}

fn output_translator(o: Output<bool>, sclk: bool) -> OutputPins {
    match o {
        Output::UpdateMode => OutputPins$(sclk, cs: true, mosi: true),
        Output::FrameInv(val) => OutputPins$(sclk, cs: true, mosi: val),
        Output::AllClear => OutputPins$(sclk, cs: true, mosi: false),
        Output::Dummy => OutputPins$(sclk, cs: true, mosi: false),
        Output::Address(val) => OutputPins$(sclk, cs: true, mosi: val),
        Output::Pixel(val) => OutputPins$(sclk, cs: true, mosi: val),
        Output::CsHigh => OutputPins$(sclk: false, cs: true, mosi: false),
        Output::CsLow => OutputPins$(sclk: false, cs: false, mosi: false),
    }
}

// Private stuff

struct Timing {
    us1: int<10>,
    us3: int<10>,
    mhz1: int<10>
}

// Using int<9> to avoid having to worry about negative numbers
enum State {
    CsLow{line: int<9>, duration: int<10>},
    CsHigh{line: int<9>, duration: int<10>},
    Mode{line: int<9>},
    FrameInv{line: int<9>},
    Clear{line: int<9>},
    FirstDummy{line: int<9>, count: int<5>},
    Address{line: int<9>, count: int<5>},
    Data{line: int<9>, x: int<10>},
    EndDummy{line: int<9>, count: int<5>},
    EndCsh{line: int<9>, duration: int<10>},
}


struct Clk {
    sclk: bool,
    sclk_falling_edge: bool
}

entity clk_gen(clk: clock, rst: bool, t: Timing) -> Clk {
    let one: int<2> = 1;
    reg(clk) ctr reset(rst: 0) = {
        if ctr == t.mhz1 {
            0
        }
        else {
            let ctr_: int<10> = ctr + one; 
            let ctr_: int<10> = trunc(ctr_);
            ctr_
        }
    };

    Clk$(
        sclk: ctr > (t.mhz1 `div_pow2` 1),
        sclk_falling_edge: ctr == 0
    )
}

entity display_fsm(clk: clock, rst: bool, sclk_falling_edge: bool, t: Timing) -> State {
    let one: int<2> = 1;
    reg(clk) state reset(rst: State::CsLow(0, 0)) = match (sclk_falling_edge, state) {
        (_, State::CsLow$(line, duration)) => {
            let us1_: int<11> = t.us1 - one;
            let us1_: int<10> = trunc(us1_);
            if duration == us1_ {
                State::CsHigh$(line, duration: 0)
            }
            else {
                let duration_: int<11> = duration+one;
                let duration_: int<10> = trunc(duration_);
                State::CsLow$(line, duration: duration_)
            }
        },
        (_, State::CsHigh$(line, duration)) => {
            let us1_: int<11> = t.us1 - one;
            let us1_: int<10> = trunc(us1_);
            if duration == us1_ {
                if sclk_falling_edge {State::Mode(line)} else {state}
            }
            else {
                let duration_: int<11> = duration+one;
                let duration_: int<10> = trunc(duration_);
                State::CsLow$(line, duration: duration_)
            }
        },
        (true, State::Mode(line)) => State::FrameInv(line),
        (true, State::FrameInv(line)) => State::Clear(line),
        (true, State::Clear(line)) => State::FirstDummy$(line, count: 0),
        (true, State::FirstDummy$(line, count: 4)) => State::Address$(line, count: 0),
        (true, State::FirstDummy$(line, count)) => {
          let count_: int<6> = count + one;
          let count_: int<5> = trunc(count_);
          State::FirstDummy$(line, count: count_)
        },
        (true, State::Address$(line, count: 7)) => State::Data$(line, x: 0),
        (true, State::Address$(line, count)) => {
          let count_: int<6> = count + one;
          let count_: int<5> = trunc(count_);
          State::FirstDummy$(line, count: count_)
        },
        (true, State::Data$(line, x: 399)) => State::EndDummy$(line, count: 0),
        (true, State::Data$(line, x)) => {
          let x_: int<11> = x + one;
          let x_: int<10> = trunc(x_);
          State::Data$(line, x: x_)
        },
        (true, State::EndDummy$(line, count: 15)) => State::EndCsh$(line, duration: 0),
        (true, State::EndDummy$(line, count)) => {
          let count_: int<6> = count+one;
          State::EndDummy$(line, count: trunc(count_))
        },
        (_, State::EndCsh$(line, duration)) => {
            let us1_: int<11> = t.us1 - one;
            let us1_: int<10> = trunc(us1_);
            if duration >= us1_ {
                let line_: int<10> = line + one;
                let line_: int<9> = trunc(line_);
                let new_line = if line == 239 { 0 } else { line_ };
                State::CsLow$(line: new_line, duration: 0)
            }
            else {
                let duration_: int<11> = duration+one;
                let duration_: int<10> = trunc(duration_);
                State::EndCsh$(line, duration: duration_)
            }
        },
        (false, _) => state,
    };

    state
}


fn display_output(state: State) -> Output<(int<10>, int<9>)> {
    match state {
        State::CsLow(_, _) => Output::CsLow(),
        State::CsHigh(_, _) => Output::CsHigh(),
        State::Mode(_) => Output::UpdateMode(),
        State::FirstDummy(_, _) => Output::Dummy(),
        State::FrameInv(line) => Output::FrameInv((line & 1) == 1),
        State::Clear(_) => Output::AllClear(),
        State::Address(line, count) => Output::Address(((line >> zext(count)) & 1) == 1),
        State::Data$(line, x) => Output::Pixel((x, line)),
        State::EndDummy(_, _) => Output::Dummy(),
        State::EndCsh(_, _) => Output::CsHigh(),
    }
}
